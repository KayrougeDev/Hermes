plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'maven-publish'
}

group 'fr.kayrouge'
version plugin_version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url "https://maven.mohistmc.com/" }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
}

configurations {
    shade
    compileClasspath.extendsFrom shade
}

dependencies {
    implementation 'com.mohistmc:mohistdev:1.16.5'
    shade fileTree(dir: 'lib', include: ['*.jar'])
    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.3.0"

    shade 'net.kyori:adventure-api:4.19.0'
    shade 'net.kyori:adventure-platform-bukkit:4.3.4'

    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")
}

shadowJar {
    archiveBaseName.set(project.name)
    archiveVersion.set("")
    configurations = [project.configurations.shade]
    relocate 'net.kyori', 'hermes.libs.kyori'
}

processResources {
    inputs.property "version", project.version
    filesMatching("plugin.yml") {
        expand "version": project.version
    }
}